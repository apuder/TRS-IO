Name      TRS-IO-M1-U9;
Partno    ATF16V8B;
Date      08/03/2020;
Rev       01;
Designer  AP;
Company   SFSU;
Assembly  None;
Location  None;
Device    g16v8a;

/* Inputs  */
Pin 1   = ADDR_TRS_IO;
Pin 2   = ADDR_FREHD;
Pin 3   = ADDR_0;
Pin 4   = ADDR_1;
Pin 5   = ADDR_2;
Pin 6   = !RD_N;
Pin 7   = !WR_N;
Pin 8   = !IN_N;
Pin 9   = !OUT_N;
Pin 11  = !ESP_WAIT_RELEASE_N;

/* Outputs */
Pin 12  = WAIT;
Pin 13 = !READ_N;
Pin 14 = !ESP_SEL_N;
Pin 15 = !DBUS_SEL_N;
Pin 16 = S0;
Pin 17 = S1;
Pin 18 = !SRAM_OE_N;
Pin 19 = !SRAM_WE_N;

/* Equations */

/* Memory access for addresses >= 0x8000 */
sram_sel = ADDR_0 & (RD_N # WR_N);

SRAM_OE_N = sram_sel;
SRAM_WE_N = WR_N & ADDR_0;

/* TRS-IO */
trs_io_sel = ADDR_TRS_IO & (IN_N # OUT_N);

/* FreHD */
frehd_sel = ADDR_FREHD & (IN_N # OUT_N);

/* Floppy */
fdc_sel = ADDR_1 & RD_N;

/* ESP selected */
esp_sel = trs_io_sel # frehd_sel # fdc_sel;
ESP_SEL_N = esp_sel;

/* Data Bus selected */
DBUS_SEL_N = esp_sel # sram_sel;
READ_N = RD_N # IN_N;

/* ESP_WAIT_RELEASE_N is generated by the ESP */
WAIT = esp_sel & ESP_WAIT_RELEASE_N;

/*
    fdc_sel    == 1  -> S1:0, S0:1
    trs_io_sel == 2  -> S1:1, S0:0
    frehd_sel  == 3  -> S1:1, S1:1
*/
S0 = fdc_sel # frehd_sel;
S1 = trs_io_sel # frehd_sel;
