Name      TRS-IO-M1-U8;
Partno    ATF22V10C;
Date      08/03/2020;
Rev       01;
Designer  AP;
Company   SFSU;
Assembly  None;
Location  None;
Device    g22v10;

/*  For the TL866II+ programmer, choose profile ATF22V10C (UES) */

/* Inputs  */
Pin 1 = A10;
Pin 2 = A12;
Pin 3 = A13;
Pin 4 = A15;
Pin 5 = A11;
Pin 6 = A14;
Pin 7 = A8;
Pin 8 = A9;
Pin 9 = A0;
Pin 10 = A1;
Pin 11 = A4;
Pin 13 = A2;
Pin 14 = A6;
Pin 15 = A7;
Pin 16 = A5;
Pin 17 = A3;

/* Outputs */
Pin 18 = ADDR_1;
Pin 19 = ADDR_0;
Pin 20 = ADDR_FREHD;
Pin 21 = ADDR_TRS_IO;

/* Equations */

/* SRAM */
ADDR_0 = A15;

/* Floppy 37E0H, 37ECH, 37EFH */
fdc_base = !A15 & !A14 & A13 & A12 & !A11 & A10 & A9 & A8 & A7 & A6 & A5 & !A4;
ADDR_1 = fdc_base & ((A3 & A2 & !A1 & !A0) # (A3 & A2 & A1 & A0) #
                     (!A3 & !A2 & !A1 & !A0));

/* TRS-IO */
ADDR_TRS_IO = !A7 & !A6 & !A5 & A4 & A3 & A2 & A1 & A0;

/* FreHD */
ADDR_FREHD = A7 & A6 & !A5 & !A4;
